@top Program { statement* }

@skip { space | comment }

IdentifierWithParam { 
  Identifier "(" (Colour | Number) ")" 
}

Subroutine { 
  kw<"Anweisung"> Identifier 
    statement* 
  (kw<"endeAnweisung"> | "*" kw<"Anweisung">) 
}

Condition { 
  kw<"Bedingung"> Identifier 
    statement* 
  (kw<"endeBedingung"> | "*" kw<"Bedingung">) 
}

If { 
  kw<"wenn"> kw<"nicht">? (Identifier | IdentifierWithParam) kw<"dann"> 
    statement* 
  (kw<"sonst"> 
    statement*)? 
  (kw<"endewenn"> | "*" kw<"wenn">)
}

While { 
  kw<"wiederhole"> (kw<"immer"> | Number kw<"mal"> | kw<"solange"> kw<"nicht">? (Identifier | IdentifierWithParam)) 
    statement* 
  (kw<"endewiederhole"> | "*" kw<"wiederhole">) 
} 

WhileEnd {
  kw<"wiederhole"> 
    statement* 
  (kw<"endewiederhole"> | "*" kw<"wiederhole">) ((kw<"solange"> | kw<"bis">) kw<"nicht">? (Identifier | IdentifierWithParam)) 
}

statement {
  Identifier |
  IdentifierWithParam |
  Subroutine |
  Condition |
  If |
  While |
  WhileEnd
}

@tokens {
  Identifier { $[a-zA-Z_äüö] $[a-zA-Z_äüö\-0-9]* }
  Number { $[0-9]+ }
  Colour { "rot" | "gelb" | "blau" | "grün" | "schwarz" }
  space { $[ \t\n\r;]+ }
  comment { "{" $[a-zA-Z_äüö \t\n\r;\-0-9]* "}" }
  
  "(" ")"

  @precedence {Colour, Number, Identifier}
}

kw<word> { @specialize[@name=Keyword]<Identifier, word> }

@detectDelim
